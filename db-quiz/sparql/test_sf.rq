PREFIX csdb:    <http://cs.dbpedia.org/property/>
PREFIX dbo:     <http://dbpedia.org/ontology/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?s
       (SAMPLE(?_label) AS ?label)
       (SAMPLE(?_description) AS ?description)
       (GROUP_CONCAT(?surfaceForm ; separator = "|") AS ?surfaceForms)
WHERE {
  {
    SELECT ?s (COUNT(?link) AS ?inDegree)
    WHERE {
      GRAPH <http://cs.dbpedia.org> {
        VALUES ?class {
          <http://dbpedia.org/ontology/Person>
          <http://dbpedia.org/ontology/Place>
          <http://dbpedia.org/ontology/Work>
        }
        ?s a ?class .
        ?link dbo:wikiPageWikiLink ?s .
      }
    }
    GROUP BY ?s
    ORDER BY DESC(?inDegree)
    OFFSET 500
    LIMIT 1000
  }
  GRAPH <http://cs.dbpedia.org> {
    ?s rdfs:label ?_label ;
      dbo:abstract ?_description .
    FILTER (!REGEX(?_label, "^(\\p{Lu}\\.?)+$") # Filter out abbreviations, such as "R.E.M.".
            &&
            !REGEX(?_label, "^.*\\d+.*$") # Eliminate digits
            &&
            (STRLEN(?_description) > 140) # Get only resources with longer descriptions. 
            &&
            langMatches(lang(?_label), "cs")
            &&
            langMatches(lang(?_description), "cs"))
    VALUES ?surfaceFormProperty {
      csdb:name
      csdb:název
      csdb:příjmení
      dbo:birthName
      dbo:wikiPageWikiLinkText
      dcterms:title
    }
    OPTIONAL {
      ?s ?surfaceFormProperty ?surfaceForm .
      FILTER (isLiteral(?surfaceForm) && STR(?surfaceForm) != "")
    }
  }
}
GROUP BY ?s
LIMIT 28
