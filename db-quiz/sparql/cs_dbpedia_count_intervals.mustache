{{!
@param Array<Array<URI, URI>> selectors
@param float                  maximum-indegree
@param float                  minimum-indegree
}}

PREFIX dbo:     <http://dbpedia.org/ontology/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?difficulty (COUNT(?subject) AS ?size) 
WHERE {
  {
    SELECT ?subject (COUNT(?link) AS ?indegree)
    WHERE {
      GRAPH <http://cs.dbpedia.org> {
        VALUES (?p ?o) {
          {{#selectors}}
          (<{{{p}}}> <{{{o}}}>)
          {{/selectors}}
        }
        ?subject ?p ?o .
        OPTIONAL {
          ?link dbo:wikiPageWikiLink ?subject .
        }
      }
    }
    GROUP BY ?subject
  }
  GRAPH <http://cs.dbpedia.org> {
    ?subject rdfs:label ?_label ;
      dbo:abstract ?_description .
    FILTER (!REGEX(?_label, "^(\\p{Lu}\\.?)+$") {{! Filter out abbreviations, such as "R.E.M.". }}
            &&
            !REGEX(?_label, "^.*\\d+.*$") {{! Eliminate digits }}
            &&
            (STRLEN(?_description) > 140) {{! Get only resources with longer descriptions. }} 
            &&
            langMatches(lang(?_label), "cs")
            &&
            langMatches(lang(?_description), "cs")
            &&
            (STRLEN(?_label) < 40)) {{! Eliminate too long labels. }} 
  }
  BIND(IF(?indegree > {{minimum-indegree}} && ?indegree < {{maximum-indegree}}, "normal",
          IF(?indegree > {{maximum-indegree}}, "easy", "hard")) AS ?difficulty)
}
GROUP BY ?difficulty
