{{!
@param Array<Array<URI, URI>> selectors
@param int                    limit
@parem int                    offset
}}

PREFIX csdb:    <http://cs.dbpedia.org/property/>
PREFIX dbo:     <http://dbpedia.org/ontology/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?subject
       ?label
       ?description
       (GROUP_CONCAT(DISTINCT ?surfaceForm ; separator = "|") AS ?surfaceForms)
WHERE {
  {
    SELECT ?subject ?label ?description
    WHERE {
      {
        SELECT ?subject
              (STR(SAMPLE(?_label)) AS ?label)
              (STR(SAMPLE(?_description)) AS ?description) 
        WHERE {
          {
            SELECT ?subject (COUNT(?link) AS ?indegree)
            WHERE {
              GRAPH <http://cs.dbpedia.org> {
                VALUES (?p ?o) {
                  {{#selectors}}
                  (<{{{p}}}> <{{{o}}}>)
                  {{/selectors}}
                }
                ?subject ?p ?o .
                OPTIONAL {
                  ?link dbo:wikiPageWikiLink ?subject .
                }
              }
            }
            GROUP BY ?subject
          }
          GRAPH <http://cs.dbpedia.org> {
            ?subject rdfs:label ?_label ;
              dbo:abstract ?_description .
            FILTER (!REGEX(?_label, "^(\\p{Lu}\\.?)+$") {{! Filter out abbreviations, such as "R.E.M.". }}
                    &&
                    (STRLEN(?_description) > 140) {{! Get only resources with longer descriptions. }} 
                    &&
                    langMatches(lang(?_label), "cs")
                    &&
                    langMatches(lang(?_description), "cs")
                    &&
                    (STRLEN(?_label) < 30)) {{! Eliminate too long labels. }}
          }
        }
        GROUP BY ?subject ?indegree
        ORDER BY DESC(?indegree)
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  OPTIONAL {
    GRAPH <http://cs.dbpedia.org> {
      VALUES ?surfaceFormProperty {
        csdb:name
        csdb:název
        csdb:příjmení
        dbo:birthName
        dbo:wikiPageWikiLinkText
        dcterms:title
      }
      ?subject ?surfaceFormProperty ?surfaceForm .
    }
  }
}
GROUP BY ?subject ?label ?description
