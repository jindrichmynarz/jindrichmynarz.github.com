{{!
@param Array<Array<URI, URI>> selectors
@param int                    limit
@parem int                    offset
}}

PREFIX dbo:     <http://dbpedia.org/ontology/>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

SELECT ?subject
       ?label
       ?description
       (GROUP_CONCAT(DISTINCT ?surfaceForm ; separator = "|") AS ?surfaceForms)
WHERE {
  {
    SELECT ?subject
    WHERE {
      {
        SELECT ?subject
        WHERE {
          GRAPH <http://dbpedia.org/db-quiz> {
            VALUES ?class {
              {{#selectors}}
              <{{{o}}}>
              {{/selectors}}
            }
            ?subject a ?class ;
              dbo:wikiPageInDegree ?indegree .
          }
        }
        GROUP BY ?subject ?indegree
        ORDER BY DESC(?indegree)
      }
    }
    LIMIT {{limit}}
    OFFSET {{offset}}
  }
  GRAPH <http://dbpedia.org/db-quiz> {
    ?subject rdfs:label ?label ;
      dbo:abstract ?description ;
      skos:altLabel ?surfaceForm .
  }
}
GROUP BY ?subject ?label ?description
